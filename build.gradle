plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=enchantmentfix"
 
            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

repositories {
  maven {
    name = "CurseMaven"
    url "https://www.cursemaven.com"
    content {
        includeGroup "curse.maven"
    }
  }
  maven {
    name = "Progwml6's maven"
    url = "https://dvs1.progwml6.com/files/maven/"
  }
  maven {
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
  }
  maven {
    name = "ModMaven"
    url = "https://modmaven.dev"
  }
}

dependencies {
	// Base
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings_version}:v2")

	// Libraries
	modImplementation("net.fabricmc:fabric-loader:${project.fabric_loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	// Extras
  	modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
  	modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
  	modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

	modImplementation("curse.maven:jade-324717:${jade_version}")
	modImplementation("curse.maven:enchantment-descriptions-250419:${enchantment_descriptions_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		mavenLocal()
	}
}